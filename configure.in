AC_INIT(src/main.cs)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(f-spot, 0.0.12)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL([0.21])

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS
AM_PATH_GLIB_2_0

dnl --- Check for mono and mcs

AC_PATH_PROG(MONO, mono)
AC_PATH_PROG(MCS, mcs)

dnl -- Check for mono pc file

AC_MSG_CHECKING([for mono.pc])
if test -z `$PKG_CONFIG --variable=prefix mono`; then
  AC_MSG_ERROR([missing the mono.pc file, usually found in the mono-devel package])
else
  AC_MSG_RESULT([found])
fi

dnl -- check for various mono DLLs that we need.

needed_dlls="Mono.Data.SqliteClient Mono.Posix System.Runtime.Remoting System.Web System.Web.Services"
for i in $needed_dlls; do
  AC_MSG_CHECKING([for $i.dll])
  if test ! -e `$PKG_CONFIG --variable=prefix mono`/lib/mono/1.0/$i.dll; then
    AC_MSG_ERROR([missing required mono DLL: $i.dll])
  else
    AC_MSG_RESULT([found])
  fi
done

dnl --- Required libraries

LIBGNOME_REQUIRED=2.2
LIBGNOMEUI_REQUIRED=2.2
LIBEXIF_REQUIRED_MIN=0.5.7
LIBEXIF_REQUIRED_MAX=0.7.0
GTKSHARP_REQUIRED=1.0

dnl -- this check is 	
LCMS_REQUIRED=1.12
dnl -- LIBGPHOTO2_REQUIRED=2.1.4

PKG_CHECK_MODULES(F, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED libexif >= $LIBEXIF_REQUIRED_MIN libexif < $LIBEXIF_REQUIRED_MAX gtkhtml-sharp >= $GTKSHARP_REQUIRED gconf-sharp >= $GTKSHARP_REQUIRED glade-sharp >= $GTKSHARP_REQUIRED gtk+-2.0 >= 2.4)
AC_SUBST(F_CFLAGS)
AC_SUBST(F_LIBS)


dnl --- GConf

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
AM_GCONF_SOURCE_2


dnl --- libjpeg

AC_CHECK_LIB(jpeg, jpeg_start_decompress, [],
	     [AC_MSG_ERROR([libjpeg not found])], -lm)
AC_CHECK_HEADER(jpeglib.h, , [AC_MSG_ERROR([libjpeg not found])])
LIBJPEG='-ljpeg'


dnl --- Sqlite

AC_CHECK_LIB(sqlite, sqlite_last_insert_rowid, [],
	     [AC_MSG_ERROR([sqlite not found])])


dnl --- lcms

PKG_CHECK_MODULES(LCMS, lcms >= $LCMS_REQUIRED, [], 
	[AC_CHECK_LIB(lcms, cmsCreateBCHSWabstractProfile, [],
		     [AC_MSG_ERROR([liblcms not found])], -lm)
	AC_CHECK_HEADER(lcms.h, , [AC_MSG_ERROR([liblcms not found])])
	LCMS_LIBS='-llcms'
	LCMS_CFLAGS=''])


dnl --- libgphoto2-sharp

PKG_CHECK_MODULES(LIBGPHOTO2, libgphoto2 >= 2.1.4)

dnl --- libexif version check

LIBEXIF_API_CHANGE=0.6.0
PKG_CHECK_MODULES(LIBEXIF_API_CHECK, libexif >= $LIBEXIF_API_CHANGE, have_old_libexif=no, have_old_libexif=yes)

if test "x$have_old_libexif" = "xyes"; then
	EXIF_SOVERSION=9
else
	PKG_CHECK_MODULES(LIBEXIF_VERSION_CHECK, libexif >= 0.6.12, EXIF_SOVERSION=12, EXIF_SOVERSION=10)
fi
AC_SUBST(EXIF_SOVERSION)

dnl --- Set up paths


ICONS_DIRECTORY=${datadir}/${PACKAGE}-${VERSION}

AC_SUBST(ICONS_DIRECTORY)

dnl -- Intl

ALL_LINGUAS="bg ca cs da de en_CA en_GB es eu fr hu it ja nb nl no pl rw sv zh_CN"

GETTEXT_PACKAGE=f-spot
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package])
AM_GLIB_GNU_GETTEXT

dnl --- Prologue

AC_SUBST(LIBJPEG)
AC_SUBST(LCMS_LIBS)
AC_SUBST(LCMS_CFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

expanded_libdir=`( case $prefix in NONE) prefix=$ac_default_prefix ;; *) ;; esac
		   case $exec_prefix in NONE) exec_prefix=$prefix ;; *) ;; esac
		   eval echo $libdir )`
AC_SUBST(expanded_libdir)

AC_CONFIG_FILES([src/f-spot],[chmod +x src/f-spot])

AC_OUTPUT([
Makefile
icons/Makefile
libeog/Makefile
libeog/cursors/Makefile
libjpegtran/Makefile
libfspot/Makefile
libgphoto2-sharp/Makefile
po/Makefile.in
src/Defines.cs
src/f-spot.exe.config
src/Makefile
f-spot.spec
f-spot.desktop.in
])
